AC_INIT([dovecot-managesieve], [0.10.3], [dovecot@dovecot.org], [dovecot-1.1-managesieve])
AC_CONFIG_SRCDIR([src])

AC_CONFIG_HEADERS([dmanagesieve-config.h])
AM_INIT_AUTOMAKE

AM_MAINTAINER_MODE

AC_PROG_CC
AC_PROG_CPP
AM_PROG_LIBTOOL

AC_ARG_WITH(dovecot,
[  --with-dovecot[=DIR]    Dovecot base directory (../)],
	dovecotdir="$withval",
	dovecotdir=../dovecot
)

AC_ARG_WITH(dovecot-sieve,
[  --with-dovecot-sieve[=DIR]    Dovecot-sieve plugin base directory (../)],
    dovecot_sievedir="$withval",
    dovecot_sievedir=../dovecot-sieve-1.1
)

top=`pwd`
cd $dovecotdir
dovecotdir=`pwd`
cd $top
cd $dovecot_sievedir
dovecot_sievedir=`pwd`
cd $top
AC_SUBST(dovecotdir)
AC_SUBST(dovecot_sievedir)

if ! test -f "$dovecotdir/dovecot-config"; then
  echo
  echo "dovecot-config not found from $dovecotdir, use --with-dovecot=PATH"
  echo "to give path to compiled Dovecot sources or to a directory with the"
  echo "installed dovecot-config file."
  AC_MSG_ERROR([dovecot-config not found])
fi

if test -d "$dovecotdir/src"; then
  # compiling against sources
  have_dovecot_libs=yes
else
  # compiling against installed headers
  have_dovecot_libs=no
fi
AM_CONDITIONAL(HAVE_DOVECOT_LIBS, test "$have_dovecot_libs" = "yes")

if test -f "$dovecot_sievedir/src/sieve-cmu.c"; then
  # compiling against old cmu sieve plugin
  echo "compiling against old dovecot-sieve plugin."
  SIEVE_PLUGIN_INCLUDE=$top/src/lib-cmusieve
  SIEVE_PLUGIN_LIB=$top/src/lib-cmusieve/libsieve_cmu.la
  have_old_sieve_plugin=yes
else
  if test -f "$dovecot_sievedir/src/lib-sieve/sieve.c"; then
    # compiling against native dovecot sieve plugin
    echo "compiling against new dovecot-sieve plugin."
    SIEVE_PLUGIN_INCLUDE=$dovecot_sievedir/src/lib-sieve
    SIEVE_PLUGIN_LIB=$dovecot_sievedir/src/lib-sieve/libsieve.la
    have_old_sieve_plugin=no
  else
    echo
    echo "Dovecot Sieve implementation not found from $dovecot_sievedir, "
    echo "use --with-dovecot-sieve=PATH to give path to compiled Dovecot Sieve sources."
    AC_MSG_ERROR([dovecot-sieve not found])
  fi
fi
AM_CONDITIONAL(HAVE_OLD_SIEVE_PLUGIN, test "$have_old_sieve_plugin" = "yes")

dnl replace relative ../ paths in the file with full paths
eval `cat $dovecotdir/dovecot-config|sed 's,\$(top_builddir)/,$dovecotdir/,g'`

if test $have_dovecot_libs = yes; then
  dovecot_incdir="$dovecotdir"
fi

AC_SUBST(STORAGE_LIBS)
AC_SUBST(LIBICONV)
AC_SUBST(RAND_LIBS)
AC_SUBST(MODULE_LIBS)
AC_SUBST(SSL_LIBS)
AC_SUBST(dovecot_incdir)
AC_SUBST(moduledir)

AC_SUBST(SIEVE_PLUGIN_INCLUDE)
AC_SUBST(SIEVE_PLUGIN_LIB)

AC_CONFIG_FILES([
Makefile
src/Makefile
src/lib-sievestorage/Makefile
src/lib-managesieve/Makefile
src/managesieve/Makefile
src/managesieve-login/Makefile
src/lib-cmusieve/Makefile
stamp.h])

AC_OUTPUT
